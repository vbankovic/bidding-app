plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'org.asciidoctor.jvm.convert' version "3.2.0"
}

group = 'com.homework'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    unitTestImplementation.extendsFrom testImplementation
    unitTestRuntimeOnly.extendsFrom testRuntimeOnly
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    jcenter()
    mavenCentral()
}

sourceSets {
    unitTest {
        java.srcDir "$projectDir/src/unitTest/java"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task unitTest(type: Test) {
    testClassesDirs = sourceSets.unitTest.output.classesDirs
    classpath = sourceSets.unitTest.runtimeClasspath
}

check.dependsOn unitTest

asciidoctor {
    sourceDir = file('src/docs/asciidoc')
    sources {
        include 'api-guide.adoc'
    }
    attributes 'snippets': file('build/generated-snippets')
    outputDir = file('build/asciidoc/html5')

}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

tasks.unitTest {
    useJUnitPlatform()
}
